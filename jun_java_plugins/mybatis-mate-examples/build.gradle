// 编译脚本
buildscript {
    ext {
        springBootVersion = "2.6.2"
        mybatisPlusVersion = "3.5.0"
    }

    // 仓库配置
    repositories {
        mavenLocal()
        maven { url 'https://maven.aliyun.com/repository/central' }
        maven { url 'https://maven.aliyun.com/repository/spring' }
        maven { url 'https://maven.aliyun.com/repository/google' }
        maven { url 'https://repo.spring.io/release' }
        maven { url 'https://repo.spring.io/milestone' }
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

description = "MybatisPlus 企业模块版"

// 项目配置
allprojects {
    group "mybatis.mate"
    version "1.0"

}

// 子模块配置
subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: "io.spring.dependency-management"
    apply plugin: 'org.springframework.boot'

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
        options.warnings = false
    }

    // 仓库配置
    repositories {
        mavenLocal()
        maven { url 'https://maven.aliyun.com/repository/central' }
        maven { url 'https://maven.aliyun.com/repository/spring' }
        maven { url 'https://maven.aliyun.com/repository/google' }
        maven { url 'https://repo.spring.io/release' }
        maven { url 'https://repo.spring.io/milestone' }
    }

    // 依赖管理
    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
        }

        dependencies {
            dependency("com.baomidou:mybatis-plus-boot-starter:${mybatisPlusVersion}")
            dependency("com.baomidou:mybatis-plus-core:${mybatisPlusVersion}")
            dependency("com.baomidou:mybatis-plus-extension:${mybatisPlusVersion}")
            dependency("com.github.jsqlparser:jsqlparser:4.2")
            dependency("org.modelmapper:modelmapper:2.4.5")
            dependency("org.aspectj:aspectjweaver:1.9.7")
            dependency("org.jasypt:jasypt:1.9.3")
            dependency("org.javers:javers-core:6.5.3")
            dependency("com.h2database:h2:1.4.200")
            dependency("org.postgresql:postgresql:42.3.1")
            dependency("mysql:mysql-connector-java:8.0.26")
            // 编译测试
            dependency("javax.servlet:javax.servlet-api:3.1.0")
            dependency("junit:junit:4.12")
        }
    }

    // 依赖配置
    dependencies {
        // 日志
        api("org.slf4j:slf4j-api")

        api("com.baomidou:mybatis-mate-starter:1.2.4")

        api("com.baomidou:mybatis-plus-boot-starter")
        api("org.springframework.boot:spring-boot-starter-web")
        api("org.springframework.boot:spring-boot-starter-jdbc")

        // lombok
        compileOnly("org.projectlombok:lombok")
        annotationProcessor("org.projectlombok:lombok")
        testAnnotationProcessor("org.projectlombok:lombok")
        testCompileOnly("org.projectlombok:lombok")
    }

    // 编译环境 JDK1.8
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}
